ngh MOORE
ticks 300

global double rate = 0.15;
global int incubation = 3;
global int duration = 4;
global double density = 0.7;
global double fatality = 0.2;
global double immunity = 0.5;
global int medIntroduced = 300;
global double medEffectiveness = 0.0;
global int qIntroduced = 300;
global double qEffectiveness = 0.0;


cell() {
    int cincubation = 0;
    int cduration = 0;
    double cimmunity = 0;
    bool cmedicated = false;
    bool cquarantined = false;

    state SINFECTED     (1.0, 0.0, 0.0)
    state NO_INFECTIOUS (1.0, 0.8, 0.6)
    state RECOVER       (1.0, 0.0, 1.0)
    state QUARANTINE    (0.6, 0.8, 1.0)
    state DIE           (0.0, 0.0, 0.0)
}


strain NAME1(){
    // if cell is empty or not infected, go to the next cell
    if ( state == NO_CHANGE || infected == false ) {
        continue;
    }

    if (infected == true ) {
        if (cincubation > 0){
            cincubation = cincubation - 1;
        } else {
            state = SINFECTED;
            if ( cduration > 0 ) {
                cduration = cduration - 1;
            } else {
                if (random > fatality) {
                    state = RECOVER;
                    infected = false;
                    cincubation = 0;
                    cduration = 0;
                    cimmunity = immunity;
                } else {
                    state = DIE;
                    infected = false;
                    cduration = 0;
                }
            }

        }
    }

    // if cell has recovered or still incubating, go to next cell
    if ( infected == false || cincubation > 0) {
       continue;
    }

    // apply medicine if it's time
    if (cmedicated == false && (currentTick > medIntroduced)) {
        if (random < medEffectiveness) {
		    state = RECOVER;
            infected = false;
            cincubation = 0;
            cduration = 0;
            cimmunity = immunity;
        } else {
            // the cell has been medicated but it didn't work
            cmedicated = true;
        }
    }

    // if medicine is successful, cell is recovered so go to next cell
    if (infected == false) {
        continue;
    }

    // quarantine has been started
    if (cquarantined == false && (currentTick > qIntroduced) && (random < qEffectiveness)) {
        state = QUARANTINE;
	    cquarantined = true;
    }


    if (cquarantined == false && count(infected, true) >= 1) {
        if (state == RECOVER){
            if (random > cimmunity){
                // if probability less than infection rate then cell gets infected
                if (random < rate) {
                    state = NO_INFECTIOUS;
                    infected = true;
                    cincubation = incubation;
                    cduration = duration;
                }
            }
        } else if (state == NO_CHANGE){
            state = NO_INFECTIOUS;
            infected = true;
            cincubation = incubation;
            cduration = duration;
        }
    }

}


init() {
    state = NO_INFECTIOUS;
    cduration   = duration;
}